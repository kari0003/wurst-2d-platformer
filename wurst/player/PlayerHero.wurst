package PlayerHero
import Entity
import DummyRecycler
import KeyUtils
import ObjectData
import Textures

public class PlayerHero extends Entity
    let objectData = new ObjectData()
    let dummyUnit = DummyRecycler.get(ZERO2, angle(0))

    var acceleration = ZERO2
    var onGround = false

    construct(player owner)
        super(owner, ZERO2 + vec2(256, 384))
        sleeps = false
        objectData.texturePath = Textures.icon_file
        objectData.size = vec2(64, 64)
        objectData.pos = pos.toVec2()

    function jump()
        if onGround
            vel.y = 15
            onGround = false

    override function update()
        pos.x = objectData.pos.x
        pos.y = objectData.pos.y
        onGround = objectData.onGround
        super.update()
        dummyUnit.setXY(pos)
        objectData.pos = pos.toVec2()

        if not onGround
            vel.y += gravity
        else
            vel.y = 0.

        let downKeys = owner.getDownKeys()
        if downKeys.left and not downKeys.right
            acceleration = LEFT * 10
        else if downKeys.right and not downKeys.left
            acceleration = RIGHT * 10
        else
            acceleration = ZERO2

        if downKeys.up and onGround
            jump()

        vel.x = vel.x.lerp(acceleration.x, 0.5)
