package LevelData
import public ObjectData
import HashMap
import HashList

/*
    The workflow idea is as follows:
        * Create new LevelData
        * Populate with TileData tiles using setter
            -> The tiles should be assigned pathability and texture at creation
        * Add all dynamic objects using #addObject
            -> The objects themselves must be loaded before
        * Finished LevelData is passed to the Render and Collision systems
*/

/** The pathing status of a tile */
public enum Pathing
    FREE
    BLOCKED
    WATER // TODO

/** Row and column index of a tile in the 2d grid */
public tuple tilePos(int x, int y)

/** Model data for one tile of the tilemap */
public class TileData
    var tilePos = tilePos(0, 0)
    var pathing = Pathing.FREE
    var texturePath = ""

    construct()

    construct(Pathing pathing, string texture)
        this.pathing = pathing
        this.texturePath = texture

/** Model data for one level */
public class LevelData
    /** Stores the tile grid */
    private let tileMap = new HashMap<int, TileData>
    /** Stores level entities which aren't constrained to grid */
    private let objectList = new HashList<ObjectData>
    /** How many horizontal tiles */
    private var width = 32
    /** How many vertical tiles */
    private var height = 32
    /** Tile where players start */
    private var startingTile = tilePos(1, 4)
    /** Size of an individual tile */
    private var tileSize = 96.

    construct()

    construct(int width, int height)
        this.width = width
        this.height = height

    function setStartingTile(int x, int y)
        this.startingTile = tilePos(x, y)

    function setTile(int x, int y, TileData tile)
        tileMap.put(x + width * y, tile)

    function getTile(int x, int y) returns TileData
        return tileMap.get(x + width * y)

    function getWidth() returns int
        return width

    function getHeight() returns int
        return height

    function getTileSize() returns real
        return tileSize

    function addObject(ObjectData object)
        objectList.add(object)

    function getObjects() returns HashList<ObjectData>
        return objectList

function TileData.assertEquals(TileData other)
    (this castTo int).assertEquals(other castTo int)

@Test function testLevelData()
    let level = new LevelData(8, 8)
    for i = 0 to 7
        for j = 0 to 7
            let tileData = new TileData()
            level.setTile(i, j, tileData)
            level.getTile(i, j).assertEquals(tileData)

    level.addObject(new ObjectData())
